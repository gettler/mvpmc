#!/usr/bin/python
#
# audio/video plugin
#

Import ('env')

dir = env['BUILD_DIR']
target = env['TARG']
libdir = env['INSTLIBDIR']
ldflags = env['LDFLAGS']
linkmode = env['LINKMODE']
bindir = env['INSTBINDIR']

if linkmode == 'dynamic':
	env.Append(CCFLAGS = ' -DPLUGIN_SUPPORT')
	
common = [ 'av.c' ]

if target == 'host':
	src = [ common, 'av_host.c' ]
	libs = ''
elif target == 'mg35':
	src = [ common, 'av_mg35.c' ]
	libs = ''
elif target == 'nmt':
	src = [ common, 'av_nmt.c' ]
	libs = ''
elif target == 'mvphd':
	src = [ common, 'av_mvphd.c' ]
	libs = ''
elif target == 'mvp':
	src = [ common, 'av_mvp.c' ]
	libs = [ 'libav', 'libdemux' ]

static = env.StaticLibrary('mvpmc_av.plugin.a', src,
                           CPPPATH = [env['INCDIR'], env['INSTINCDIR']],
			   LIBS = libs,
			   LIBPATH = [ libdir ],
                           LINKFLAGS = ldflags)

if linkmode == 'dynamic':
    shared = env.SharedLibrary('mvpmc_av.plugin', src,
                               CPPPATH = [env['INCDIR'], env['INSTINCDIR']],
			       LIBS = libs,
			       LIBPATH = [ libdir ],
                               LINKFLAGS = ldflags)
    inst = env.Install(libdir, [ static, shared ])
else:
    inst = env.Install(libdir, [ static ])


Return('inst')
