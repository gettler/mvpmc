#!/usr/bin/python
#
# http plugin
#

Import ('env')

dir = env['BUILD_DIR']
target = env['TARG']
libdir = env['INSTLIBDIR']
ldflags = env['LDFLAGS']
linkmode = env['LINKMODE']
bindir = env['INSTBINDIR']

if linkmode == 'dynamic':
	env.Append(CCFLAGS = ' -DPLUGIN_SUPPORT')
	
if target == 'mg35':
	mhlib = libdir + '/libmicrohttpd.a'
	clib = libdir + '/libcurl.a'
elif (target == 'nmt') or (target == 'mvphd'):
	mhlib = libdir + '/libmicrohttpd.a'
	clib = libdir + '/libcurl.so'
else:
	mhlib = libdir + '/libmicrohttpd.so'
	clib = libdir + '/libcurl.so'

src = [ 'http.c', 'client.c', 'daemon.c' ]

static = env.StaticLibrary('mvpmc_http.plugin.a', src,
                           CPPPATH = [env['INCDIR'], env['INSTINCDIR']],
			   LIBPATH = libdir,
			   LINKFLAGS = ldflags + ' ' + mhlib + ' ' + clib)
env.Depends(static, mhlib)
env.Depends(static, clib)

if linkmode == 'dynamic':
    shared = env.SharedLibrary('mvpmc_http.plugin', src,
                               CPPPATH = [env['INCDIR'], env['INSTINCDIR']],
			       LIBPATH = libdir,
                               LINKFLAGS = ldflags + ' ' + mhlib + ' ' + clib)
    env.Depends(shared, mhlib)
    env.Depends(shared, clib)
    inst = env.Install(libdir, [ static, shared ])
else:
    inst = env.Install(libdir, [ static ])

Return('inst')
