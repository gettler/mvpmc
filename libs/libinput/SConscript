#!/usr/bin/python
#
# libinput
#

Import ('env')

dir = env['BUILD_DIR']
target = env['TARG']
libdir = env['INSTLIBDIR']
ldflags = env['LDFLAGS']
gtkcflags = env['GTKCFLAGS']
gtkldflags = env['GTKLDFLAGS']
linkmode = env['LINKMODE']
bindir = env['INSTBINDIR']

common = [ 'input.c' ]

if target == 'mvp':
    src = [ common, 'ir.c' ]
    dl = ''
    pt = ''
elif target == 'mg35':
    src = [ common, 'fip.c' ]
    dl = ''
    pt = ''
elif target == 'nmt':
    src = [ common, 'nmt.c' ]
    dl = 'dl'
    pt = 'pthread'
else:
    src = [ common, 'gtk.c']
    dl = ''
    pt = ''

env.Append(CCFLAGS = ' ' + gtkcflags)

inputtest = env.Program('inputtest',
                        [ 'inputtest.c' ],
                        CPPPATH = [ env['INCDIR'], env['INSTINCDIR'], ],
                        LIBS = [ 'input', dl, pt ],
                        LIBPATH = [ libdir ],
                        LINKFLAGS = ldflags + ' ' + gtkldflags )

static = env.StaticLibrary('input', src,
			   LIBS = [ 'piutil', pt ],
			   LIBPATH = [ libdir ],
                           CPPPATH = [env['INCDIR'], env['INSTINCDIR']],
                           LINKFLAGS = ldflags)

if linkmode == 'dynamic':
    shared = env.SharedLibrary('input', src,
                               LIBS = [ 'piutil', pt ],
                               LIBPATH = [ libdir ],
                               CPPPATH = [env['INCDIR'], env['INSTINCDIR']],
                               LINKFLAGS = ldflags)
    inst = env.Install(libdir, [ static, shared ])
else:
    inst = env.Install(libdir, [ static ])

inst2 = env.Install(bindir, inputtest)
env.Depends(inputtest, inst)

Return('inst2')
