#!/usr/bin/python
#
# libosd
#

Import ('env')

dir = env['BUILD_DIR']
target = env['TARG']
libdir = env['INSTLIBDIR']
freetype = env['INSTINCDIR'] + '/freetype2'
bindir = env['INSTBINDIR']
ldflags = env['LDFLAGS']
gtkcflags = env['GTKCFLAGS']
gtkldflags = env['GTKLDFLAGS']

common = [ 'font.c', 'init.c', 'osd.c' ]

if target == 'mg35':
	src = [ common, 'mg35.c' ]
	dl = ''
elif target == 'mvp':
	src = [ common, 'cursor.c', 'gfx.c', 'fb.c' ]
	dl = 'dl'
else:
	src = [ common, 'gtk.c' ]
	dl = ''

env.Append(CCFLAGS = ' ' + gtkcflags)

static = env.StaticLibrary('osd', src,
			   CPPPATH = [ env['INCDIR'], env['INSTINCDIR'],
				       freetype, '.' ])

osdtest = env.Program('osdtest',
		      [ 'osdtest.c' ],
		      CPPPATH = [ env['INCDIR'], env['INSTINCDIR'], ],
		      LIBS = [ 'osd', 'freetype', dl ],
		      LIBPATH = [ libdir ],
		      LINKFLAGS = ldflags + ' ' + gtkldflags )

if target == 'mvp':
	shared = env.SharedLibrary('osd', src,
				   CPPPATH = [ env['INCDIR'],
					       env['INSTINCDIR'],
					       freetype, '.' ])
	inst = env.Install(libdir, [ static, shared ])
else:
	inst = env.Install(libdir, [ static ])

inst2 = env.Install(bindir, osdtest)
env.Depends(osdtest, inst)

Return('inst2')
