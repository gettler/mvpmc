#!/usr/bin/python
#
# libosd
#

Import ('env')

dir = env['BUILD_DIR']
target = env['TARG']
libdir = env['INSTLIBDIR']
freetype = env['INSTINCDIR'] + '/freetype2'
bindir = env['INSTBINDIR']
ldflags = env['LDFLAGS']
gtkcflags = env['GTKCFLAGS']
gtkldflags = env['GTKLDFLAGS']
linkmode = env['LINKMODE']

common = [ 'font.c', 'init.c', 'osd.c' ]

if target == 'mg35':
	src = [ common, 'mg35.c' ]
	dl = ''
	libs = []
elif target == 'mvp':
	src = [ common, 'cursor.c', 'gfx.c', 'fb.c' ]
	dl = 'dl'
	libs = []
elif target == 'nmt':
	src = [ common, 'dfb.c' ]
	dl = ''
	libs = [ 'directfb', 'fusion', 'direct', 'pthread' ]
else:
	src = [ common, 'gtk.c' ]
	dl = ''
	libs = []

env.Append(CCFLAGS = ' ' + gtkcflags)

static = env.StaticLibrary('osd', src,
			   CPPPATH = [ env['INCDIR'], env['INSTINCDIR'],
				       freetype, '.' ])

osdtest = env.Program('osdtest',
		      [ 'osdtest.c' ],
		      CPPPATH = [ env['INCDIR'], env['INSTINCDIR'], ],
		      LIBS = [ 'osd', 'freetype', dl ] + libs,
		      LIBPATH = [ libdir ],
		      LINKFLAGS = ldflags + ' ' + gtkldflags )

if linkmode == 'dynamic':
	shared = env.SharedLibrary('osd', src,
				   CPPPATH = [ env['INCDIR'],
					       env['INSTINCDIR'],
					       freetype, '.' ])
	inst = env.Install(libdir, [ static, shared ])
else:
	inst = env.Install(libdir, [ static ])

inst2 = env.Install(bindir, osdtest)

ret = [ inst, inst2 ]

Return('ret')
